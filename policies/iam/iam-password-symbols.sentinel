# This policy requires that the `require_symbols` attribute of the `aws_iam_account_password_policy`
# resource is according to CIS standards

# Copyright (c) HashiCorp, Inc.
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                              "iam-password-symbols",
	"resource_aws_iam_account_password_policy": "aws_iam_account_password_policy",
	"require_symbols":                          "require_symbols",
	"message":                                  "'aws_iam_account_password_policy' 리소스의 'require_symbols' 속성은 true와 같아야 합니다. 자세한 내용은 https://docs.aws.amazon.com/securityhub/latest/userguide/iam-controls.html#iam-13 을 참조하세요.",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_iam_account_password_policy).resources
violations = collection.reject(resources, func(res) {
	key = "values.require_symbols"
	return maps.has(res, key) and maps.get(res, key) is true
})

summary = {
	"policy_name": "iam-password-symbols",
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
